include dynamic/utils.corth
include linux_x86/io/output.corth
include core/stack.corth

macro sizeof(position) 24 endmacro
macro sizeof(address)  64 endmacro

macro ADDR.FILE  0  endmacro
macro ADDR.PREV  8  endmacro
macro ADDR.START 16 endmacro
macro ADDR.END   40 endmacro

macro POS.LINE   0  endmacro
macro POS.CHAR   8  endmacro
macro POS.AT     16 endmacro

macro LOG.INFO  "\x1b[1;97m[INFO]\x1b[0;97m "    endmacro
macro LOG.WARN  "\x1b[1;33m[WARNING]\x1b[0;97m " endmacro
macro LOG.SOFT  "\x1b[1;31m[SOFT]\x1b[0;97m "    endmacro
macro LOG.ERR   "\x1b[1;31m[ERROR]\x1b[0;97m "   endmacro
macro LOG.BUG   "\x1b[1;31m[BUG]\x1b[0;97m "     endmacro

macro LOG.CMD   "\x1b[1;32m[CMD]\x1b[0;97m "     endmacro
macro LOG.TODO  "\x1b[1;35m[TODO]\x1b[0;97m "    endmacro
macro LOG.HERE  "\x1b[1;32m[HERE]\x1b[0;97m "    endmacro


// This buffer can be used to easily write many spaces at once.
// NOTE: Trying to write more than .-BUFFER-SIZE characters may cause problems.
macro  SPACE-BUFFER-SIZE 256               endmacro
memory space-buffer      SPACE-BUFFER-SIZE end

macro  TILDE-BUFFER-SIZE 256               endmacro
memory tilde-buffer      TILDE-BUFFER-SIZE end


proc debug-init -> in
  // Set up the space-buffer.
  space-buffer SPACE-BUFFER-SIZE ' ' memset8

  // Set up the tilde-buffer.
  tilde-buffer TILDE-BUFFER-SIZE '~' memset8
end


proc generate-position
  // int: line-no int: char-no int: at -> ptr: position
  int int int -> ptr
in
  sizeof(position) malloc let position in
    position isn-null if
      // Store the arguments.
      position POS.AT   + !64
      position POS.CHAR + !64
      position POS.LINE + !64
    else
      // Remove the unused arguments.
      drop drop drop
    end

    // Return the position.
    position
  end
end


proc generate-address
  // ptr: file-name ptr: previous int: start-line int: start-char int: start-at int: end-line int: end-char int: end-at -> ptr: address
  ptr ptr int int int int int int -> ptr
  // Returns a dynamicly-placed object to store the address of token.
in
  sizeof(address) malloc let address in
    address isn-null if
      // Store the arguments.
      address ADDR.END   + POS.AT   + !64
      address ADDR.END   + POS.CHAR + !64
      address ADDR.END   + POS.LINE + !64
      address ADDR.START + POS.AT   + !64
      address ADDR.START + POS.CHAR + !64
      address ADDR.START + POS.LINE + !64
      address ADDR.PREV  + !64
      address ADDR.FILE  + !64
    else
      // Remove the unused arguments.
      drop drop drop drop drop drop drop drop
    end

    // Return the address.
    address
  end
end


proc fput-position
  // ptr: position file-desc: file-desc
  ptr file-desc ->
  // Prints a position.
in let position file in
  position POS.LINE + @64 file fputu ":" file fputs position POS.CHAR + @64 file fputu " (" file fputs position POS.AT + @64 file fputu ")" file fputs
end end


proc seek-line
  // int: line file-desc: file-desc
  int file-desc ->
  // Shifts the file pointer by a specified amount of lines.
in let file in
  memory char 1 in
    while dup isn-zero do
      // If EOF is reached, returns immediately.
      char 1 file fgets is-zero if drop return end

      // If a newline is reached, decrease the counter.
      char @8 '\n' = if dec end
    end drop
  end
end end


proc fput-line
  // file-desc: source file-desc: file-desc -> int: line-length
  file-desc file-desc -> int
  // Keeps printing until a newline is found.
  // Returns the length of line read.
in let source file in
  memory char 1 in
    0 while true do
      // If EOF is reached, return immediately.
      char 1 source fgets is-zero if return end

      // If a newline character is reached, return immediately.
      char @8 '\n' = if return end

      // Print the byte to the file stream.
      char 1 file fputs
    inc end
  end
end end


macro fputu-full let _value_ _file_ in
  // int: int file-desc: file-desc

  memory _buffer_ 20 in
    _buffer_ _value_ intconvud let _buffer_start_ in
      _buffer_ _buffer_start_ _buffer_ - ' ' memset8
    end

    _buffer_ 20 _file_ fputs
  end
end endmacro


proc fput-address
  // ptr: address file-desc: log-stream
  ptr file-desc ->
  // Prints the formatted address of a token.
in let address log-stream in
  address ADDR.FILE + @64 let file-name in
    "file '" log-stream fputs file-name file-name mlength log-stream fputs "' [" log-stream fputs address ADDR.START + log-stream fput-position "]-[" log-stream fputs address ADDR.END + log-stream fput-position "]" log-stream fputs
  end
end end


proc fput-lines
  // ptr: address file-desc: log-stream
  ptr file-desc ->
  // Prints the formatted lines of a token.
in let address log-stream in
  address ADDR.FILE + @64 address ADDR.START + address ADDR.END + let file-name start-pos end-pos in
    // Add a null character and open the file.
    file-name null-term8 fopen-r let file in
      // Check if file could be opened.
      file isn-pos if
        "<file-unavailable>\n" log-stream fputs
        return
      end

      start-pos POS.LINE + @64 start-pos POS.CHAR + @64 end-pos POS.LINE + @64 end-pos POS.CHAR + @64 let start-line start-char end-line end-char in

        // Move the file pointer to the start.
        file 0 SEEK-SET lseek drop start-line dec file seek-line

        start-line end-line = if
          start-line log-stream fputu-full ": " log-stream fputs file log-stream fput-line log-stream fputnl let length in
            "                      " log-stream fputs space-buffer start-char log-stream fputs tilde-buffer end-char start-char - log-stream fputs log-stream fputnl
          end
        else
          start-line log-stream fputu-full ": " log-stream fputs file log-stream fput-line log-stream fputnl let length in
            "                      " log-stream fputs space-buffer start-char log-stream fputs tilde-buffer length start-char - log-stream fputs log-stream fputnl
          end

          start-line inc while dup end-line < do
            dup log-stream fputu-full ": " log-stream fputs file log-stream fput-line log-stream fputnl let length in
              "                      " log-stream fputs tilde-buffer length log-stream fputs log-stream fputnl
            end
          inc end drop

          end-line log-stream fputu-full ": " log-stream fputs file log-stream fput-line log-stream fputnl let length in
            "                      " log-stream fputs tilde-buffer end-char log-stream fputs log-stream fputnl
          end
        end

        // Close the file.
        file fclose drop
      end
    end
  end
end end


proc fput-lines-position
  // ptr: position ptr: file-name file-desc: log-stream
  ptr ptr file-desc ->
  // Prints the formatted linees of a token.
in let position file-name log-stream in
  // Add a null character for fopen.
  file-name null-term8 fopen-r let file in
    // Check if the file could be opened.
    file isn-pos if
      "<file-unavailable>\n" log-stream fputs
      return
    end

    position POS.LINE + @64 position POS.CHAR + @64 position POS.AT + @64 let line-no char-no at in
      // Move the file pointer to the start,
      file 0 SEEK-SET lseek drop line-no dec file seek-line

      line-no log-stream fputu-full ": " log-stream fputs file log-stream fput-line log-stream fputnl let length in
        "                      " log-stream fputs space-buffer char-no log-stream fputs tilde-buffer 1 log-stream fputs log-stream fputnl
      end
    end

    // Close the file.
    file fclose drop
  end
end end


proc fput-here
  // ptr: address file-desc: log-stream ->
  ptr file-desc ->
in let address log-stream in
  LOG.HERE log-stream fputs address log-stream fput-address ":\n" log-stream fputs
  address log-stream fput-lines

  address ADDR.PREV + @64 let prev in
    prev isn-null if
      prev log-stream fput-here
    end
  end
end end


macro fput-here-position let _position_ _file_name_ _log_stream_ in
  // ptr: position file-desc: log-stream ->
  LOG.HERE _log_stream_ fputs "At " _log_stream_ fputs _position_ _log_stream_ fput-position " in '" _log_stream_ fputs _file_name_ _file_name_ mlength _log_stream_ fputs "':\n" _log_stream_ fputs
  _position_ _file_name_ _log_stream_ fput-lines-position
end endmacro


macro fput-inside-procedure let _procedure_name_ _procedure_address_ _log_stream_ in
  // ptr: _address_ ptr: _procedure_name_ ptr: _procedure_address_ file-desc: _log_stream_ ->
  LOG.INFO _log_stream_ fputs "Inside procedure '" _log_stream_ fputs _procedure_name_ _procedure_name_ mlength _log_stream_ fputs "'.\n" _log_stream_ fputs
  LOG.HERE _log_stream_ fputs _procedure_address_ _log_stream_ fput-address ":\n" _log_stream_ fputs

  _procedure_address_ _log_stream_ fput-lines
end endmacro


proc report-bug
  // file-desc: file-desc ->
  file-desc ->
in let log-stream in
  LOG.ERR log-stream fputs "This should have been impossible to reach.\n" log-stream fputs
  LOG.INFO log-stream fputs "This is probably caused by a bug in the compiler.\n" log-stream fputs
end end
