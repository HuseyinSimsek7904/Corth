include Corth/compiler/stack_item.corth


proc is-signed-dword
  int -- bool
in let value in
  value 0x7fffffff <= value 0x7fffffff neg >= &
end end


proc fput-alpha
  ptr file-desc --
in let name file in
  name mlength malloc let object in
    object is-null if
      "'malloc' failed while allocating space for fput-alpha.\n" STDERR fputs
      ERROR-EXIT-CODE exit drop
    end

    0 while dup name mlength < do let i in
      name i + @8 let char in
        char 'a' 'z' is-range char 'A' 'Z' is-range | char '1' '9' is-range | if
          char object i + !8
        else
          '_' object i + !8
        end
      end
    i end inc end drop

    object object mlength file fputs

    object mfree drop
  end
end end


proc nasm.level
  // int: id file-desc: output-file
  int file-desc --
in let output-file in
  ".level_" output-file fputs output-file fputu ":\n" output-file fputs
end end


proc nasm.jump-level
  // int: id file-desc: output-file
  int file-desc --
in let output-file in
  "    jmp     .level_" output-file fputs output-file fputu output-file fputnl
end end


proc nasm.jump-zero-level
  // int: id file-desc: output-file
  int file-desc --
in let output-file in
  "    jz      .level_" output-file fputs output-file fputu output-file fputnl
end end


proc nasm.mov-reg-zero
  // ptr: register-name int: register-name-length file-desc: output-file
  ptr int file-desc --
in let register-name register-name-length output-file in
  "    xor     " output-file fputs register-name register-name-length output-file fputs
  ", " output-file fputs register-name register-name-length output-file fputs output-file fputnl
end end


proc nasm.mov-reg-immediate
  // int: immediate ptr: register-name int: register-name-length file-desc: output-file
  int ptr int file-desc --
in let immediate register-name register-name-length output-file in
  immediate is-zero if
    register-name register-name-length output-file nasm.mov-reg-zero
  else
    "    mov     " output-file fputs register-name register-name-length output-file fputs
    ", " output-file fputs immediate output-file fputi output-file fputnl
  end
end end


proc nasm.mov-reg-global-address
  // ptr: global-name ptr: register-name int: register-name-length file-desc: output-file
  ptr ptr int file-desc --
in let global-name register-name register-name-length output-file in
  "    mov     " output-file fputs register-name register-name-length output-file fputs
  ", global_" output-file fputs global-name output-file fput-alpha output-file fputnl
end end


proc nasm.mov-reg-@localconst
  // int: offset ptr: register-name int: register-name-length file-desc: output-file
  int ptr int file-desc --
in let offset register-name register-name-length output-file in
  "    mov     " output-file fputs register-name register-name-length output-file fputs ", [local_ptr]\n" output-file fputs

  "    mov     " output-file fputs register-name register-name-length output-file fputs
  ", [" output-file fputs register-name register-name-length output-file fputs
  offset isn-zero if
    " + " output-file fputs offset output-file fputu 
  end
  "]\n" output-file fputs
end end


proc nasm.mov-reg-local
  // int: offset ptr: register-name int: register-name-length file-desc: output-file
  int ptr int file-desc --
in let offset register-name register-name-length output-file in
  "    mov     " output-file fputs register-name register-name-length output-file fputs ", [local_ptr]\n" output-file fputs

  offset isn-zero if
    "    add     " output-file fputs register-name register-name-length output-file fputs ", " output-file fputs offset output-file fputu output-file fputnl
  end
end end


proc nasm.mov-reg-data
  // int: index ptr: register-name int: register-name-length file-desc: output-file
  int ptr int file-desc --
in let index register-name register-name-length output-file in
  "    mov     " output-file fputs register-name register-name-length output-file fputs
  ", data_" output-file fputs index output-file fputu output-file fputnl
end end


proc nasm.mov-reg-item
  // ptr: item ptr: register-name int: register-name-length file-desc: output-file
  ptr ptr int file-desc --
in let item register-name register-name-length output-file in      
  item get-mode MODE-REAL = if
    "    pop     " output-file fputs register-name register-name-length output-file fputs output-file fputnl
  else item get-mode MODE-IMM = if
    item get-arg1 register-name register-name-length output-file nasm.mov-reg-immediate
  else item get-mode MODE-GLOBAL = if
    item get-arg1 register-name register-name-length output-file nasm.mov-reg-global-address
  else item get-mode MODE-@LOCALCONST = if
    item get-arg1 register-name register-name-length output-file nasm.mov-reg-@localconst
  else item get-mode MODE-LOCAL = if
    item get-arg1 register-name register-name-length output-file nasm.mov-reg-local
  else item get-mode MODE-DATA = if
    item get-arg1 register-name register-name-length output-file nasm.mov-reg-data
  else
    LOG.ERR STDERR fputs "Unknown integer mode while trying to move integer to register.\n" STDERR fputs
    STDERR report-bug
    ERROR-EXIT-CODE exit drop
  end end end end end end
end end


proc nasm.push-int-immediate
  // int: immediate file-desc: output-file
  int file-desc --
  // NOTE: Uses R15.
in let immediate output-file in
  immediate is-signed-dword if
    "    push    qword " output-file fputs immediate output-file fputi output-file fputnl
  else
    "    mov     r15, " output-file fputs immediate output-file fputi output-file fputnl
    "    push    r15\n" output-file fputs
  end
end end


proc nasm.push-global-address
  // ptr: global-name file-desc: output-file
  ptr file-desc --
in let global output-file in
  "    push    global_" output-file fputs global output-file fput-alpha output-file fputnl
end end


proc nasm.push-@localconst
  // int: offset file-desc: output-file
  int file-desc --
in let offset output-file in
  "    mov     r15, [local_ptr]\n" output-file fputs

  offset is-zero if
    "    push    qword [r15]\n" output-file fputs
  else
    "    push    qword [r15 + " output-file fputs offset output-file fputu "]\n" output-file fputs
  end
end end


proc nasm.push-local
  // int: offset file-desc: output-file
  int file-desc --
in let offset output-file in
  "    push    qword [local_ptr]\n" output-file fputs

  offset isn-zero if
    "    add     qword [rsp], " output-file fputs offset output-file fputu output-file fputnl
  end
end end


proc nasm.push-data
  // int: index file-desc: output-file
  int file-desc --
in let index output-file in
  "    push    qword data_" output-file fputs index output-file fputu output-file fputnl
end end


proc nasm.push-int
  // ptr: item file-desc: output-file
  ptr file-desc --
in let item output-file in
  item get-mode MODE-IMM = if
    item get-arg1 output-file nasm.push-int-immediate
    
  else item get-mode MODE-GLOBAL = if
    item get-arg1 output-file nasm.push-global-address

  else item get-mode MODE-@LOCALCONST = if
    item get-arg1 output-file nasm.push-@localconst

  else item get-mode MODE-LOCAL = if
    item get-arg1 output-file nasm.push-local

  else item get-mode MODE-DATA = if
    item get-arg1 output-file nasm.push-data

  else item get-mode MODE-REAL != if
    LOG.ERR STDERR fputs "Unknown integer mode while trying to push an integer item.\n" STDERR fputs
    STDERR report-bug
    ERROR-EXIT-CODE exit drop
  end end end end end end
end end


proc nasm.push-bool
  // ptr: item file-desc: output-file
  ptr file-desc --
in let item output-file in
  item get-mode MODE-@LOCALCONST = if
    item get-arg1 output-file nasm.push-@localconst
    
  else item get-mode MODE-REAL != if
    LOG.ERR STDERR fputs "Unknown bool mode while trying to push a boolean item.\n" STDERR fputs
    STDERR report-bug
    ERROR-EXIT-CODE exit drop
  end end
end end


proc nasm.push
  // ptr: item file-desc: output-file
  ptr file-desc --
in let item output-file in
  item output-file
       item get-type INT-TYPE-INT  = if nasm.push-int
  else item get-type INT-TYPE-BOOL = if nasm.push-bool
  else drop drop
    LOG.ERR STDERR fputs "Unknown intrinsic type to push\n" STDERR fputs
    STDERR report-bug
    ERROR-EXIT-CODE exit drop
  end end
end end


proc nasm.convert-int-to-real
  // ptr: item file-desc: output-file
  ptr file-desc --
in let item output-file in
  item output-file nasm.push-int

  MODE-REAL item set-mode
end end


proc nasm.convert-bool-to-real
  // ptr: item file-desc: output-file
  ptr file-desc --
in let item output-file in
  item output-file nasm.push-bool

  MODE-REAL item set-mode
end end


proc nasm.convert-item-to-real
  // ptr: item file-desc: output-file
  ptr file-desc --
in let item output-file in
  item output-file
       item get-type INT-TYPE-INT  = if nasm.convert-int-to-real
  else item get-type INT-TYPE-BOOL = if nasm.convert-bool-to-real
  else drop drop
    LOG.ERR STDERR fputs "Unknown intrinsic type to convert to real.\n" STDERR fputs
    STDERR report-bug
    ERROR-EXIT-CODE exit drop
  end end
end end


proc nasm.add-int-immediate
  // int: immediate file-desc: output-file
  int file-desc --
in let immediate output-file in
  immediate is-zero if return end

  immediate is-neg if
    immediate is-signed-dword if
      "    sub     qword [rsp], " output-file fputs immediate neg output-file fputu output-file fputnl
    else
      "    mov     r15, " output-file fputs immediate neg output-file fputu output-file fputnl
      "    sub     qword [rsp], rax\n" output-file fputs
    end
    
  else
    immediate is-signed-dword if
      "    add     qword [rsp], " output-file fputs immediate output-file fputu output-file fputnl
    else
      "    mov     r15, " output-file fputs immediate output-file fputu output-file fputnl
      "    add     qword [rsp], rax\n" output-file fputs
    end
  end
end end
