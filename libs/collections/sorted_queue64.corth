include libs/collections/queue64.corth
include libs/memory.corth
include libs/core.corth


/*
  To create an empty sorted queue, an array with the preferred size and a pointer should be created.
  After that, the queue should be initialized using queue64.init.

  Also, a regular queue can be sorted using queue64.sort which will convert that queue to a sorted queue.

  Values at smaller indexes are less than the values at bigger indexes.
*/


proc queue64.sort
  // ptr: queue-start ptr: *queue-length
  ptr ptr --
  // Sorts an array using bubble sort ( :( )
in
  @64 let queue-start queue-length in
    queue-length dec while dup isn-zero do let i in
      0 while dup i < do let j in
        queue-start j 8 * + @64 queue-start j inc 8 * + @64 let x y in
          x y > if
            y queue-start j     8 * + !64
            x queue-start j inc 8 * + !64
          end
        end
      j end inc end drop
    i end dec end drop
  end
end


proc squeue64.append
  // int: value ptr: queue-start ptr: *queue-length
  int ptr ptr --
  // Appends an item to a sorted array
in
  let value queue-start *queue-length in
    memory left  sizeof(int) and
           right sizeof(int) in

      1 neg left !64
      *queue-length @64 right !64

      while right @64 left @64 - dec isn-zero do
        left @64 right @64 + 2 / let middle in
          value middle queue-start *queue-length queue64.get > if
            middle left !64
          else
            middle right !64
          end
        end
      end

      value right @64 queue-start *queue-length queue64.insert
    end
  end
end
