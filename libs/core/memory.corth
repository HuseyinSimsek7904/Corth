include libs/core/ptr.corth
include libs/core/arithmetic.corth
include libs/collections/array8.corth
include libs/collections/array64.corth


// TODO: This should be changed to ptr type, when 'ptr' type is implemented for real.
macro NULLPTR 0 endmacro


// ptr: x -- bool: f(x)
macro is-null  NULLPTR =  endmacro
macro isn-null NULLPTR != endmacro


proc memcmp8
  // ptr: a-src ptr: b-src int: len -- int: cmp(a, b)
  ptr ptr int -- int
  // Returns <0 if a<b.
  // Returns  0 if a=b.
  // Returns >0 if a>b.
in let a-src b-src len in
  0 while dup len < do let i in
    i a-src array8.get i b-src array8.get - let cmp in
      cmp isn-zero if
        cmp return
      end
    end
  i end inc end drop
end 0 end

proc memcmp64
  // ptr: a-src ptr: b-src int: len -- int: cmp(a, b)
  ptr ptr int -- int
  // Returns <0 if a<b.
  // Returns  0 if a=b.
  // Returns >0 if a>b.
in let a-src b-src len in
  0 while dup len < do let i in
    i a-src array64.get i b-src array64.get - let cmp in
      cmp isn-zero if
        cmp return
      end
    end
  i end inc end drop
end 0 end

proc memcmp8-wl
  // ptr: a-src int: a-len ptr: b-src int: b-len -- int: cmp(a, b)
  ptr int ptr int -- int
  // Returns <0 if a<b.
  // Returns  0 if a=b.
  // Returns >0 if a>b.
in let a-src a-len b-src b-len in
  a-src b-src a-len b-len min2 memcmp8 let cmp in
    cmp isn-zero if
      cmp return
    end

    a-len b-len -
  end
end end

proc memcmp64-wl
  // ptr: a-src int: a-len ptr: b-src int: b-len -- int: cmp(a, b)
  ptr int ptr int -- int
  // Returns <0 if a<b.
  // Returns  0 if a=b.
  // Returns >0 if a>b.
in let a-src a-len b-src b-len in
  a-src b-src a-len b-len min2 memcmp64 let cmp in
    cmp isn-zero if
      cmp return
    end

    a-len b-len -
  end
end end


proc memset8
  // ptr: start int: length int: value
  ptr int int --
  // Sets every item in a collection to a value.
in let start length value in
  start length +
  let finish in
    start while dup finish < do
      let i in
        value i !8
      i end
    inc end drop
  end
end end


proc memset64
  // ptr: start int: length int: value
  ptr int int --
  // Sets every item in a collection to a value.
in let start length value in
  start length 8 * +
  let finish in
    start while dup finish < do
      let i in
        value i !64
      i end
    8 + end drop
  end
end end


// Decreases a value in the memory. (*-- operator)
macro @dec8  let _a_ in _a_ @8  dec _a_ !8  end endmacro // ptr: x
macro @dec64 let _a_ in _a_ @64 dec _a_ !64 end endmacro // ptr: x


// Increases a value in the memory. (*++ operator)
macro @inc8  let _a_ in _a_ @8  inc _a_ !8  end endmacro // ptr: x
macro @inc64 let _a_ in _a_ @64 inc _a_ !64 end endmacro // ptr: x


macro @swp8
  // ptr: a ptr: b
  // Swaps two bytes in the memory.
  // *a, *b = *b, *a
  let _a_ _b_ in
    _a_ @8 _b_ @8 _a_ !8 _b_ !8
  end
endmacro


macro @swp64
  // ptr: a ptr: b
  // Swaps two 8-byte values in the memory.
  // *a, *b = *b, *a
  let _a_ _b_ in
    _a_ @64 _b_ @64 _a_ !64 _b_ !64
  end
endmacro


proc memcpy8
  // ptr: src ptr: dst int: len 
  // Copies a part of memory to another location.
  // Bytes are read one-by-one.
  ptr ptr int --
in
  let src dst len in
    0 while dup len < do let i in
      src i + @8 
      dst i + !8
    i end inc end drop
  end
end


// memcpy64 only works a bit faster, no other functionality is changed.
// If the length is known to be divisible by 8, it is recommended to use memcpy64.
proc memcpy64
  // ptr: src ptr: dst int: len
  // Copies a part of memory to another location.
  // Bytes are read 8-by-8.
  ptr ptr int --
in
  let src dst len in
    0 while dup len 8 * < do let i in
      src i + @64
      dst i + !64
    i end 8 + end drop
  end
end


proc memcpy8i
  // ptr: src ptr: dst int: len
  // Copies a part of memory to another location.
  // Bytes are read one-by-one.
  // The copying is done in reverse order, aka from last item to first item.
  ptr ptr int --
in
  let src dst len in
    len while dup isn-zero do dec let i in
      src i + @8
      dst i + !8
    i end end drop
  end
end


proc memcpy64i
  // ptr: src ptr: dst int: len
  // Copies a part of memory to another location.
  // Bytes are read 8-by-8.
  // The copying is done in reverse order, aka from last item to first item.
  ptr ptr int --
in
  let src dst len in
    len 8 * while dup isn-zero do 8 - let i in
      src i + @64
      dst i + !64
    i end end drop
  end
end


proc memswp8
  // ptr: a ptr: b int: len
  // Swaps two regions of memory.
  // Swaps one byte per iteration.
  ptr ptr int --
in
  let a b len in
    0 while dup len < do let i in
      a i + b i + @swp8
    i end inc end drop
  end
end


// memswp64 only works a bit faster, no other functionality is changed.
// If the length is known to be divisible by 8, it is recommended to use memswp64.
proc memswp64
  // ptr: a ptr: b int: len
  // Swaps two regions of memory.
  // Swaps 8 bytes per iteration.
  ptr ptr int --
in
  let a b len in
    0 while dup len 8 * < do let i in
      a i + b i + @swp64
    i end 8 + end drop
  end
end
